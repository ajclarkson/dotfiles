# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles
export ZSH_PLUGINS=$HOME/.dotfiles/zsh-plugins

# all of our zsh files
typeset -U config_files
config_files=($ZSH/zsh/**/*.zsh)
plugin_files=($ZSH_PLUGINS/**/*.plugin.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# load compinit for autocomplete
autoload -U compinit
compinit

# load completion files
for file in ${(M)config_files:#*/completion.zsh)}
do
  source $file
done

unset config_files

# load custom extras

extras=~/.extras
extraConf=($extras/**/*.zsh)

for file in ${extraConf}
do
  source ${file}
done

for file in ${plugin_files}
do
    source $file
done

# Enable Ctrl-x-e to edit command line
autoload -U edit-command-line
# Emacs style
zle -N edit-command-line
bindkey '^xe' edit-command-line
bindkey '^x^e' edit-command-line
